#!@WHICHPERL@
=head1 NAME

fimo_webservice - Run fimo in a restricted mode and create an index webpage.

=head1 SYNOPSIS

fimo_webservice [options] <motifs> <db seqs>

  Options:
    -upseqs <file>    uploaded sequences
    -pvthresh <pv>    output p-value threshold
    -norc             scan given strand only
    -help             brief help message

=cut

use strict;
use warnings;

use Fcntl qw(SEEK_SET);
use File::Basename qw(fileparse);
use File::Spec::Functions qw(catfile tmpdir);
use File::Temp qw(tempfile);
use Getopt::Long;
use Pod::Usage;
use Time::HiRes qw(gettimeofday tv_interval);

use lib qw(@PERLLIBDIR@);

use ExecUtils qw(stringify_args invoke);
use MemeWebUtils qw(is_safe_name add_status_msg update_status loggable_date write_invocation_log);
use Globals;

# Globals
my $t0 = [&gettimeofday()];
# constants
my $tmpdir = '@TMP_DIR@';
# use the perl default if none is supplied or the replace fails
$tmpdir = &tmpdir() if ($tmpdir eq '' || $tmpdir =~ m/^\@TMP[_]DIR\@$/);
my $dbdir = '@MEMEDB@/fasta_databases';

# variables for the service invocation log
my $log_args = stringify_args(@ARGV);
my $log_date = loggable_date();
my $log_file = 'fimo-log';
my $timeout = $Globals::MAXTIME; # updated just before use
my $oot = 0; #FALSE

# error files
my $messages = "messages.txt";

#status page
my $file_list;
my @arg_errors = ();
my $msg_list = [];
my $program = 'FIMO';
my $page = 'index.html';
my $refresh = 10;

# required parameters
my $motifs;
my $dbseqs;
# option defaults
my $upseqs = undef;
my $pvthresh = undef;
my $norc = 0; #FALSE
my $help = 0; #FALSE
# derived defaults
my $seqs;
my $bfile = undef;

add_status_msg('Parsing arguments', $msg_list);

# redirect stderr so we can get the error message from GetOpts
my ($err_old, $err_tmp, $opts_ok, $opts_msg);
open($err_old, ">&STDERR") or die("Can't dup STDERR: $!");
$err_tmp = tempfile('GetOptions_XXXXXXXXXX', DIR => $tmpdir, UNLINK => 1); # make a temporary file
open(STDERR, '>&', $err_tmp) or die("Can't redirect STDERR: $!");
# parse options
$opts_ok = GetOptions(
  'upseqs=s' => \$upseqs,
  'pvthresh=f' => \$pvthresh,
  'norc' => \$norc,
  'help|?' => \$help
);
($motifs, $dbseqs) = @ARGV;
# display help
pod2usage(1) if $help;
# reset stderr and get the error message if any
open(STDERR, ">&", $err_old) or die("Can't reset STDERR: $!");
seek($err_tmp, 0, SEEK_SET);
while ($opts_msg = <$err_tmp>) {
  chomp($opts_msg);
  push(@arg_errors, $opts_msg);
}
close($err_tmp);

# remove any path from the files to ensure they are in this directory
unless (defined($motifs)) {
  push(@arg_errors, "No motifs provided.");
} else {
  $motifs = fileparse($motifs);
  if (not is_safe_name($motifs)) {
    push(@arg_errors, "Motifs \"$motifs\" does not fit allowed file name pattern.");
  } elsif (not -e $motifs) {
    push(@arg_errors, "Motifs \"$motifs\" does not exist.");
  } 
}
if (defined($upseqs)) {
  $upseqs = fileparse($upseqs);
  if (not is_safe_name($upseqs)) {
    push(@arg_errors, "Value \"$upseqs\" invalid for option upseqs (does not fit allowed file name pattern)");
  } elsif (not -e $upseqs) {
    push(@arg_errors, "Value \"$upseqs\" invalid for option upseqs (file does not exist)");
  } else {
    $seqs = $upseqs;
  }
} elsif (defined($dbseqs)) {
  $dbseqs = fileparse($dbseqs);
  if (-e catfile($dbdir, $dbseqs)) {
    $seqs = catfile('db', $dbseqs);
    # get the background file if it exists
    $bfile = $seqs . '.bfile' if (-e (catfile($dbdir, $dbseqs) . '.bfile'));
  } else {
    push(@arg_errors, "Database \"$dbseqs\" does not exist in the database directory \"$dbdir\"");
  }
} else {
  push(@arg_errors, "No sequences provided.");
}
if (defined($pvthresh)) {
  if ($pvthresh <= 0 || $pvthresh > 1) {
    push(@arg_errors, "Value \"$pvthresh\" invalid for option pvthresh (not a valid p-value)");
  }
}

$opts_ok = 0 if (scalar(@arg_errors) > 0);
foreach my $arg_error (@arg_errors) {
  print STDERR $arg_error, "\n";
  add_status_msg($arg_error, $msg_list);
}

# setup status page
$file_list = [
    {file => 'fimo.html', desc => 'FIMO html output'},
    {file => 'fimo.xml', desc => 'FIMO xml output'}, 
    {file => 'cisml.xml', desc => 'FIMO CISML output'},
    {file => 'fimo.gff', desc => 'FIMO GFF output'},
    {file => 'fimo.txt', desc => 'FIMO plain text output'},
    {file => $motifs, desc => 'Input Motifs'},
    {file => $upseqs, desc => 'Uploaded Sequence Database'}
];

if ($opts_ok) {
  add_status_msg('Arguments ok', $msg_list);
} else {
  add_status_msg("Error parsing arguments", $msg_list);
}

update_status($page, $program, ($opts_ok ? $refresh : 0), $file_list, 
    $msg_list, ($opts_ok ? "Starting" : "Error"));

# exit if there was an error reading the arguments
unless ($opts_ok) {
  write_invocation_log($log_file, $log_date, $log_args);
  pod2usage(2);
}

# Run FIMO
my @fimo_args = ('--oc', '.', '--verbosity', 1);
push(@fimo_args, '--bgfile', $bfile) if (defined($bfile));
push(@fimo_args, '--thresh', $pvthresh) if (defined($pvthresh));
push(@fimo_args, '--norc') if ($norc);
push(@fimo_args, $motifs, $seqs);
add_status_msg('Starting fimo<br><code>' . stringify_args('fimo', @fimo_args) . '</code>', $msg_list);

update_status($page, $program, $refresh, $file_list, $msg_list, "Starting");

my ($time, $status_code);
# create the symlink
symlink($dbdir, 'db') unless $upseqs;
# run the program
$timeout = $Globals::MAXTIME - int(&tv_interval($t0, [&gettimeofday()]) + 0.5);
$status_code = invoke(
  PROG => 'fimo', 
  ARGS => \@fimo_args, 
  BIN => '@BINDIR@', 
  ALL_FILE => $messages, 
  TIME => \$time,
  TIMEOUT => $timeout,
  OOT => \$oot);
# remove the simlink
unlink('db') unless $upseqs;

my $status_msg;
if ($oot) {
  $status_msg = "Ran out of time! Stopping fasta-get-markov.";
  add_status_msg($status_msg, $msg_list);
  print STDERR $status_msg;
}
if ($status_code != 0 || $oot) {
  if ($status_code == -1) {
    $status_msg = "fimo failed to run";
  } elsif ($status_code & 127) {
    $status_msg = "fimo process died with signal " . 
        ($status_code & 127) . ", " . 
        (($status_code & 128) ? 'with' : 'without') . " coredump";
  } else {
    $status_msg = "fimo exited with error code " . ($status_code >> 8);
  }
  print STDERR $status_msg;
  push(@{$file_list}, {file => $messages, desc => 'Error Messages'});
} else {
  $status_msg = 'fimo ran successfully in ' . 
      (int($time * 100 + 0.5) / 100) . ' seconds';
  push(@{$file_list}, {file => $messages, desc => 'Warning Messages'});
}
add_status_msg($status_msg, $msg_list);

update_status($page, $program, 0, $file_list, $msg_list, 
    ($status_code ? "Error" : "Done"));


write_invocation_log($log_file, $log_date, $log_args);
exit(1) if ($status_code || $oot);
1;

